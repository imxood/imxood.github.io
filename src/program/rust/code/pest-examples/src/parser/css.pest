WHITESPACE = _{ " " | "\r"? ~ "\n" }
COMMENT = _{ "/*" ~ (!"*/" ~ ANY)* ~ "*/" }

// css = _{ SOI ~ (object | array) ~ EOI }

/* number */

uint8 = @{"0" | "1" ~ ASCII_DIGIT ~ ASCII_DIGIT | "2" ~ '0'..'4' ~ ASCII_DIGIT | "25" ~ '0'..'5' | ASCII_NONZERO_DIGIT ~ ASCII_DIGIT? }
hex_u8 = @{ ASCII_HEX_DIGIT ~ ASCII_HEX_DIGIT }
int = @{ "-"? ~ ("0" | ASCII_NONZERO_DIGIT ~ ASCII_DIGIT*) }
float = @{ int ~ ("." ~ ASCII_DIGIT*)? }
positive_number = @{ ("0" | ASCII_NONZERO_DIGIT ~ ASCII_DIGIT*) ~ ("." ~ ASCII_DIGIT*)? }

px = ${ float ~ "px" }
em = ${ float ~ "em" }
rem = ${ float ~ "rem" }
mm = ${ float ~ "mm" }
cm = ${ float ~ "cm" }
inch = ${ float ~ "in" }

length  = ${ px | em | rem| mm | cm  | inch }
percentage = ${ float ~ "%" }
length_percentage = ${ length | percentage }

/* some symbols */

thin = { "thin" }
medium = { "medium" }
thick = { "thick" }

none = { "none" }
dotted = { "dotted" }
dashed = { "dashed" }
solid = { "solid" }
double = { "double" }
groove = { "groove" }
ridge = { "ridge" }
inset = { "inset" }
outset = { "outset" }

inline = ${ "inline" }

center = @{ "center" }
start = @{ "start" }
end = @{ "end" }

row = @{ "row" }
row_reverse = @{ "row_reverse" }
column = @{ "column" }
column_reverse = @{ "column_reverse" }

nowrap = @{ "nowrap" }
wrap = @{ "wrap" }
wrap_reverse = @{ "wrap_reverse" }

/* some functions */

rgb = { "rgb(" ~ uint8 ~ "," ~ uint8 ~ "," ~ uint8 ~ ")" }
rgba = { "rgba(" ~ uint8 ~ "," ~ uint8 ~ "," ~ uint8 ~ "," ~ float  ~ ")" }
hex = { "#" ~ hex_u8{3}  }

line_width = { length | thin | medium | thick }
border_width = { line_width{1, 4} }
border_style = { none | dotted | dashed | solid | double | groove | ridge | inset | outset }

align = ${ start | center | end }

flex_grow = ${ positive_number }
direction = ${ row | row_reverse | column | column_reverse }
wrap_type = ${ nowrap | wrap | wrap_reverse }

// 颜色
color = { rgb | rgba | hex }

// 主轴的方向
flow = { "flow:" ~ direction ~ wrap_type ~ ";"? }

// 每一个元素 在主轴上的对齐方式
justify_items = { "justify_items:" ~ align ~ ";"? }

// 所有元素作为整体 在主轴上的对齐方式
justify_content = { "justify_content:" ~ align ~ ";"? }

// 每一个元素 在纵轴上的对齐方式
align_items = { "align_items:" ~ align ~ ";"? }

// 所有元素作为整体 在纵轴上的对齐方式
align_content = { "align_content:" ~ align ~ ";"? }

// 单个子元素 在纵轴上的对齐方式
align_item = { align }

// flex布局
flex_layout = { "flex(" ~ (flow | justify_items | justify_content | align_items | align_content)* ~ ")" }

/* box model */

width = ${ length_percentage }
height = ${ length_percentage }
padding = ${ length_percentage{1, 4} }
margin = ${ length_percentage{1, 4} }
border = { (border_width | border_style | color)+ }

/* layouts */

display = { inline? ~ flex_layout }

flex = { (flex_grow | align_item)+ }

/* css file */
name = @{ (ASCII_ALPHA | ASCII_DIGIT | "_")+ }
id = @{ name }
class_name = @{ name }
tagname = @{ name }
star = @{ "*" }
selector = { (("#" ~ id) | (".") ~ class_name | tagname | star)+ }
selectors = { selector ~ ("," ~ selector)* }
properties = {
    "{" ~ (
        // box model
        "width:" ~ width ~ ";"? |
        "height:" ~ height ~ ";"? |
        "padding:" ~ padding ~ ";"? |
        "margin:" ~ margin ~ ";"? |
        "border:" ~ border ~ ";"? |
        // layout
        "display:" ~ display ~ ";"? |
        "flex:" ~ flex ~ ";"?
    )+  ~ "}"
}

entity = {
    selectors ~ properties
}

entities = {
    SOI ~ entity+ ~ EOI
}